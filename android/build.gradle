buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

subprojects {
    def configureAndroid = {
        if (project.extensions.findByName("android") != null) {
            try {
                // Fix Namespace
                if (project.android.namespace == null) {
                    def manifestFile = project.android.sourceSets.main.manifest.srcFile
                    if (manifestFile.exists()) {
                        def manifest = new XmlSlurper().parse(manifestFile)
                        def packageName = manifest.@package.text()
                        if (packageName) {
                            println "Setting namespace for ${project.name} to $packageName"
                            project.android.namespace = packageName
                        }
                    }
                }
                // Fix Java Compatibility
                project.android {
                    compileOptions {
                        sourceCompatibility JavaVersion.VERSION_17
                        targetCompatibility JavaVersion.VERSION_17
                    }
                }
            } catch (Exception e) {
                println "Failed to configure android for ${project.name}: ${e.message}"
            }
        }
    }

    if (project.state.executed) {
        configureAndroid()
    } else {
        project.afterEvaluate {
            configureAndroid()
        }
    }
}
